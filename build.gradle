buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'

group = "ca.spottedleaf.starlight"
version = mod_version + '+forge.' + lastCommitHash()
archivesBaseName = "${modid}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // add mixins
            arg "-mixin.config=" + modid + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                starlight {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // add mixins
            arg "-mixin.config=" + modid + ".mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                starlight {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', '${modid}', '--all', '--output', file('src/generated/resources/')

            mods {
                starlight {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
                "Implementation-Title": "${modid}",
                "Implementation-Version": "${version}",
                "Implementation-Vendor": "${modid}",
                "MixinConfigs": "${modid}.mixins.json",
                "MixinConnector":"ca.spottedleaf.starlight.mixin.MixinConnector"
        ])
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    compile 'org.spongepowered:mixin:0.8'
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}


processResources {
    inputs.property("version", project.version)
    filesMatching("**/mods.toml") {
        expand(
                'version': project.version
        )
    }

    filesMatching('*') {
        expand(
                'modid': project.property('modid'),
                'mod_version': project.property('mod_version'),
                'forge_version': project.property('forge_version'),
                'mc_version': project.property('mc_version'),
                'mappings_version': project.property('mappings_version')
        )
    }
}

jar.finalizedBy('reobfJar')

String lastCommitHash() {
    def out = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = out
    }
    return out.toString().trim()
}
